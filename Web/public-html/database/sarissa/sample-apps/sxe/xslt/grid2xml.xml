<?xml version="1.0"?>
<xsl:transform version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:output method="text" indent="yes"/>
	<xsl:strip-space elements="*" />
	<xsl:template match="/|*">
		<xsl:apply-templates/>
	</xsl:template>
	
	<xsl:template name="indentTmpl">
		<xsl:for-each select="ancestor::TABLE">
			<xsl:text>   </xsl:text>
		</xsl:for-each>
	</xsl:template>
	
	<!-- scrap elements -->
	<xsl:template match="TABLE[@class='element']">
		<xsl:variable name="indent">
			<xsl:call-template name="indentTmpl"/>
		</xsl:variable>
		<xsl:if test="string($indent)">
			<xsl:value-of select="concat('&#10;',$indent)"/>
		</xsl:if>
		<xsl:variable name="elemName" select="TBODY/TR/TD[2]/TEXTAREA/text()"/>
		<xsl:text></xsl:text>
		<xsl:text>&lt;</xsl:text><xsl:value-of select="$elemName"/>		
		<xsl:for-each select="TBODY/TR/TD[2]/DIV/TABLE[@class='namespace']">
			<!-- copy xmlns declarations and attributes -->
			<xsl:text> </xsl:text><xsl:value-of select="TBODY/TR/TD[2]/TEXTAREA/text()"/>
			<xsl:text>="</xsl:text><xsl:value-of select="TBODY/TR/TD[3]/TEXTAREA/text()"/><xsl:text>"</xsl:text>
		</xsl:for-each>
		<xsl:for-each select="TBODY/TR/TD[3]/DIV/TABLE[@class='attribute']"><!--  -->
			<!-- copy xmlns declarations and attributes -->
			<xsl:text> </xsl:text><xsl:value-of select="TBODY/TR/TD[2]/TEXTAREA/text()"/>
			<xsl:text>="</xsl:text><xsl:value-of select="TBODY/TR/TD[3]/TEXTAREA/text()"/><xsl:text>"</xsl:text>
		</xsl:for-each>
		<xsl:text>&gt;</xsl:text>
		<xsl:apply-templates/>
		<xsl:value-of select="concat('&#10;',$indent)"/>
		<xsl:text>&lt;/</xsl:text><xsl:value-of select="$elemName"/><xsl:text>&gt;</xsl:text>
	</xsl:template>
	<!-- scrap comments -->
	<xsl:template match="TABLE[@class='comment']">
		<xsl:variable name="indent">
			<xsl:call-template name="indentTmpl"/>
		</xsl:variable>
		<xsl:if test="string($indent)">
			<xsl:value-of select="concat('&#10;',$indent)"/>
		</xsl:if>
		<xsl:text>&lt;!--</xsl:text>
		<xsl:value-of select="TBODY/TR/TD/TEXTAREA[@areaType='commentValue']/text()"/>
		<xsl:text>--&gt;</xsl:text>
	</xsl:template>
	<!-- scrap PIs -->
	<xsl:template match="TABLE[@class='pi']">
		<xsl:variable name="indent">
			<xsl:call-template name="indentTmpl"/>
		</xsl:variable>
		<xsl:if test="string($indent)">
			<xsl:value-of select="concat('&#10;',$indent)"/>
		</xsl:if>
		<xsl:text>&lt;?</xsl:text>
		<xsl:value-of select="TBODY/TR/TD/TEXTAREA[@areaType='piName']/text()"/>
		<xsl:text> </xsl:text>
		<xsl:value-of select="TBODY/TR/TD/TEXTAREA[@areaType='textValue']/text()"/>
		<xsl:text>?&gt;</xsl:text>
	</xsl:template>
	<!-- scrap txt nodes-->
	<xsl:template match="TABLE[@class='text']">
		<xsl:variable name="indent">
			<xsl:call-template name="indentTmpl"/>
		</xsl:variable>
		<xsl:if test="string($indent)">
			<xsl:value-of select="concat('&#10;',$indent)"/>
		</xsl:if>
		<xsl:value-of select="TBODY/TR/TD/TEXTAREA[@areaType='textValue']/text()"/>
	</xsl:template>
	<!-- decoy -->
	<xsl:template match="comment()"/>
	<xsl:template match="text()"/>
</xsl:transform>